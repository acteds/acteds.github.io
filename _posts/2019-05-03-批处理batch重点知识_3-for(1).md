---
layout: post
title: 批处理batch重点知识_3.for(1)
categories: [windows, batch]
description: 批处理重点知识笔记
keywords: windows, batch
---
# 引言  
&emsp;&emsp;本篇文章介绍批处理**for**语句的无参形式、带`/d`的子文件夹形式、带`/r`的遍历形式、带`/l`的循环形式。



# **for 循环**  
&emsp;&emsp;<font  color="red">**bat文件中for里用%%i，cmd直接输入时用%i**</font>  
## **一般格式：**  
```batch
FOR [参数] %%变量名 IN (相关文件或命令) DO 执行的命令**
```
## **无参数(子文件)**
```batch
FOR %%variable IN (set) DO command [command-parameters]
```
- **for**语句的形式变量`i`，可以换成26个字母中的任意一个，**区分大小写**，形式变量也i可以换成其他的字符，注意不要与`%0`～`%9`这10个形式变量发生冲突。  
- **in**和**do**之间的**set**表示的字符串或变量可以是一个，也可以是多个，用**空格键、跳格键、逗号、分号**或**等号分隔**；  
- **for**语句**依次提取** **set**中的每一个元素，把它的值赋予形式变量i，然后把%%i带到do后的command中参与命令的执行；直到集合中的所有元素都被遍历后才停止**for**循环.  

&emsp;&emsp;实例:  
&emsp;&emsp;显示结果分成了3行,每一行都从逗号处被切分；  
```batch
for %%I in (a,b,c) do echo %%I
```
&emsp;&emsp;直接显示`a.b.c`，**因为.号不是分隔符**  
```batch
for %%I in (a.b.c) do echo %%I
```
&emsp;&emsp;当set带有**\***或**?**号时会搜索当前工作路径相匹配的文件与文件夹路径.  
&emsp;&emsp;无参数时只会搜索当前目录下的文件，且不能匹配带隐藏属性的文件.  
&emsp;&emsp;显示当前目录下与**\*.\***相匹配的文件(不遍历子文件夹）  
```batch
for %%i in (*.*) do echo %%i
```
&emsp;&emsp;显示`d:\7-Zip\`目录下与`*.*`相匹配的文件(不遍历子文件夹）  
```batch
for %%i in (d:\7-Zip\\.\*) do @echo %%i
```
&emsp;&emsp;检测当前硬盘都有哪些分区,包括U盘和移动硬盘的分区  
```batch
@echo off
set str=c d e f g h i j k l m n o p q r s t u v w x y z
echo 当前硬盘的分区有：
for %%i in (%str%) do if exist %%i: echo %%i:
pause
```

##  **参数 /d(子文件夹)**
&emsp;&emsp;这个参数只能搜索当前目录的**子文件夹**，且不能匹配带隐藏属性的文件夹  
&emsp;&emsp;格式:  
```batch
for /d %%i in (元素集合) do 命令语句集合
```
&emsp;&emsp;当`元素集合`中 **包含有通配符?或\***时，会匹配文件夹。如果没有包含通配符，则有`/d`参数和无`/d`参数效果一样.  
&emsp;&emsp;它**仅能匹配当前目录下的第一级文件夹**，或是指定位置的文件夹的下一级文件夹，而不能匹配更深层次的子文件夹。  
&emsp;&emsp;显示c盘根目录下的所有目录(不遍历子文件夹）  
```batch
for /d %%i in (c:\\) do @echo %%i
```
&emsp;&emsp;显示当前目录下名字只有1-3个字母的目录  
```batch
for /d %%i in (???) do @echo %%i
```

## **参数 /r(遍历)**
&emsp;&emsp;遍历文件夹(包括子文件夹)，但不能列举带隐藏属性的文件夹  
&emsp;&emsp;格式:  
```batch
for /r 文件夹 %%i in (元素集合) do 命令语句集合
```
&emsp;&emsp;列举**文件夹**及该文件夹路径下所有子文件夹，并把列举出来的文件夹路径与元素集合中的每一个元素拼接成形如“文件夹路径\元素”赋值给`%%i`,并执行一次**命令语句集合**.  
&emsp;&emsp;**当“元素集合”带以点号分隔的通配符?或\*的时候**，把“元素集合”视为文件（不视为文件夹），整条语句的作用是匹配文件夹及其所有子文件夹下匹配的文件；**若不以点号分隔，则把“元素集合”视为文件夹**（不视为文件）；  
&emsp;&emsp;当省略掉文件夹时，则针对当前目录；  
&emsp;&emsp;将`d:\7zip`下的EXE文件输出  
```batch
for /r d:\7-zip %%i in (*.exe) do @echo %%i
```
&emsp;&emsp;将`d:\7zip`下的所有文件夹输出并加上**in**后面括号内的字符  
```batch
for /r d:\7-zip %%i in (任何没有通配符的字符) do @echo %%i
```
&emsp;&emsp;在`d:\7zip`列举`7z.dll`存在的目录  
```batch
for /r d:\7-zip %%i in (7z.dll) do @if exist %%i echo %%i
```
&emsp;&emsp;将`d:\7zip`下的所有文件夹输出并加上**in**内的字符,这里会输出3次  
```batch
for /r d:\7-zip %%i in (a b c) do echo %%i
```
&emsp;&emsp;**for /r /d是可以一起使用的**  
```batch
@echo off
For /r /d %%i in (\*) do echo %%i
pause\>nul
```
&emsp;&emsp;效果：  
&emsp;&emsp;显示当前目录下所有的文件夹(**包括子文件夹**)；等价于 `dir /ad /s /b`。  
&emsp;&emsp;`for /r /d `其实是对 `/d` 参数的扩展，`/d`参数本身只能处理第一层文件夹，但是加上`/r`参数后就可以处理所有的子文件夹；  
&emsp;&emsp;但`for /r /d`**依然不能处理隐藏文件夹**。  

&emsp;&emsp;这里给出使用`for /r /d`的一般条件：  
&emsp;&emsp;1. 要对文件夹进行操作（`dir /ad /s /b`可以显示，但不能对文件夹进行操作）；  
&emsp;&emsp;2. 不处理隐藏文件夹（说到底，还是`for /f` 和`dir`结合的命令更强大些）。  
  
  
&emsp;&emsp;**for /r** 与 **dir /ad /s**类似,它们有如下几个优缺点:  
&emsp;&emsp;**for /r：**  
&emsp;&emsp;优点：  
- 只通过1条语句就可以同时实现获取目录路径和处理目录路径的操作；  
- 遍历文件夹的时候，是边列举边处理的，获取到一条路径就处理一条路径，内存占用小，处理大量路径的时候不会产生停顿感；  

&emsp;&emsp;缺点：  
- 不能获取到带隐藏属性的目录，会产生遗漏；不能获取带指定属性的目录  

&emsp;&emsp;**dir /ad /s：**  
&emsp;&emsp;优点：  
- 能一次性获取带任意属性的目录，不会产生遗漏；能通过指定不同的参数获取带任意属性的目录，更具灵活性。  

&emsp;&emsp;缺点：  
- `dir /ad /s` 语句仅能获取到目录路径，若要实现进一步的处理，还需要嵌入 for /f 语句中才能实现，写法不够简洁；  
- 嵌入` for /f` 语句之后，需要写成 `for /f "delims=" %%i in ('dir /ad /b /s') do ……` 的格式，受 `for /f` 语句运行机制的制约，需要先列举完所有的路径放入内存之后，才能对每一条路径进行进一步的处理，处理大量路径时，内存占用量偏大，并且在前期会产生明显的 停顿感，用户体验度不够好；  
## **参数 /L (循环)**
&emsp;&emsp;语法:  
```batch
FOR /L %variable IN (start,step,end) DO command [command-parameters]
```
&emsp;&emsp;与C语言的for差不多. 
```c
for(int a=start;a\<=(或\>=)end;a=a+step)
```
&emsp;&emsp;详解：  
&emsp;&emsp;在这个语句中，**start**、**step**和**end**都只能取整数，正负皆可，**start**指代起始值，**step**指代步长，**end**为终止值，具体含义为：从**start**开始计数，以**step**为步长，直至最接近 **end**的那个整数值为止，这之间有多少个数，**do**后的语句就执行多少次。  
&emsp;&emsp;实际上，**start**，**step**和**end**的值可正可负，甚至为0，限制非常宽松：  
- **步长step的值不能为0；**  
- **当步长step的值为正整数时，终止值end不能小于初始值start；**  
- **当步长step的值为负整数的时候，终止值end不能大于初始值start。**

&emsp;&emsp;换而言之，必须保证in和do之间能取到一个有效的数组序列。  
```c
for /l %%i in (2,1,5) do @set /p =%%i,<nul
```
&emsp;&emsp;输出2,3,4,5,  
```c
for /l %%i in (2,1,5) do @echo %%i,
```
&emsp;&emsp;输出2,回车3,回车4,回车5,回车  
```c
for /l %%i in (2 1 5) do @echo %%i,
```
&emsp;&emsp;输出2,回车3,回车4,回车5,回车  
&emsp;&emsp;**for /r**与**goto**如何选择:  
- 当循环次数**确定**的时候，首选 `for /l` 语句，也可使用`goto`语句但不推荐；  
- 当循环次数**不确定**的时候，用`goto`语句将是唯一的选择，因为，这个时候需要用`if`之类的条件语句来判断何时结束`goto`跳转。  
