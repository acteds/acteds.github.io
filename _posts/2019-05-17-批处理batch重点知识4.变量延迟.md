---
layout: post
title: 批处理batch重点知识4.变量延迟
categories: [windows, batch]
description: 批处理重点知识笔记
keywords: windows, batch
---
# 引言  
本篇文章介绍批处理的变量延迟.  



# 变量延迟  
批处理的执行过程是**自上而下，逐条执行**.“逐条”并不等同于“逐行”。这个“条”，是一条完整的语句的意思，并不是指“一行代码”。  

在复合语句中，整个复合语句是一条完整的语句，而无论这个复合语句占用了多少行的位置。常见的复合语句有：**for**语句、**if……else**语句、用连接符 **&**、 **\|\|**和 **&&**连接的语句，用管道符号 **\|**连接的语句，以及用 **括号**括起来由多条语句组合而成的 **语句块**；  
在非复合语句中，如果该语句占据了一行的位置，则该行代码为一条完整的语句。  
例如：  
```batch
@echo off
set num=0
for /f %%i in ('dir /a-d /b *.exe') do (
set /a num+=1
echo num 当前的值是 %num%
)
echo 当前目录下共有 %num% 个exe文件
dir /a-d /b *.txt|findstr "test">nul&&(
echo 存在含有 test 字符串的文本本件
) || echo 不存在含有 test 字符串的文本文件
if exist test.ini (
echo 存在 test.ini 文件
) else echo 不存在 test.ini 文件
pause
```
上面的代码共有14行，语句只有7条，它们分别是：  
- 第1条：第1行的**echo**语句；
- 第2条：第2行的**set**语句；
- 第3条：第3、4、5、6行上的**for**复合语句；
- 第4条：第7行的**echo**语句；
- 第5条：第8、9、10行上用**&&**和**\|\|**连接的复合语句；
- 第6条：第11、12、13行上的**if……else**复合语句；
- 第7条：第14行上的**pause**语句。

在代码“逐条”执行的过程中，cmd.exe这个批处理解释器会对每条语句做一些预处理工作，这就是批处理中大名鼎鼎的**预处理机制**。  
预处理的大致情形是这样的：  
- 首先，把一条完整的语句读入内存中（不管这条语句有多少行，它们都会被一起读入）
- 然后，识别出哪些部分是命令关键字，哪些是开关、哪些是参数，哪些是变量引用……如果代码语法有误，则给出错误提示或退出批处理环境；
- 如果顺利通过，接下来，就把该条语句中所有变量引用如**`%a%`替换**成`a`代表的**实际值**。
- 当所有的预处理工作完成之后，批处理才会执行每条完整语句内部每个命令的原有功能。
- 也就是说，如果命令语句中含有变量引用，并且变量的值在命令的执行过程中被改变了，即使该条语句内部的其他地方也用到了这个变量，也不会用最新值去替换它们，因为语句在被预处理的时候，所有的变量引用都已经被替换成字符串常量了。

**`set num=0&&echo %num%`**是一条复合语句，它的含义是：把0赋予变量**`num`**，成功后，显示变量`num`的值。但实际上在语句正式执行前**`%num%`**就被预处理为空串了。  
因此有了**变量延迟扩展**语句，让变量的扩展行为延迟一下，从而获取我们想要的值。  
“变量延迟”是批处理中一个十分重要的机制，它因预处理机制而生，用于复合语句，大量使用于强大的**for**语句中。  
一般说来，使用延迟变量扩展行为，可以有如下选择：  
1. 在需要使用延迟变量扩展前使用 **`setlocal enabledelayedexpansion`** 语句,然后把变量引用的百分号改成感叹号,即**`%a%`**变成**`!a!`**  
2. 使用**call**语句延迟变量扩展，需要在原命令前加上**call**命令，并把变量引用的单层百分号对改为双层,即**`echo %a%`**变成**`call echo %%a%%`**。  

因为**call**语句使用的是**双层百分号对**，容易使人犯迷糊，所以用得较少，常用的是使用**setlocal enabledelayedexpansion** 语句（set是设置的意思，local是本地的意思，enable是能够的意思，delayed是延迟的意思，expansion是扩展的意思，合起来就是：让变量成为局部变量，并延迟它的扩展行为）。  
